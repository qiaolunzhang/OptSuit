
reset;
model Ex2_column_generation.mod;
data .\instances\i_40_1.dat;


problem master;
option solver "cplex";

problem pricing;
option solver "cplex";

#auxiliary parameters
param flag default 0;
param max_iter default 200;
param tic;
param toc;
param CG_time;


let tic := _ampl_user_time + _total_solve_user_time;

let S:= N;

for{i in 1..N}{
	
	let a[i,i] := 1;

}


printf "\n \t \t \t COLUMN GENERATION \n\n\n" > resultsCG.txt;
printf"------------------------------------------------ \n" > resultsCG.txt;

for{counter in 1 .. max_iter} 
{     
	#reset flag
	let flag := 0;
	     
    #solve the current master problem   
	solve master;
	
	#print the optimal objective value of the current master problem 
	printf"\n objective function (bins): %7.6f\n", UsedVM > resultsCG.txt;
	printf"\n used VMS: "> resultsCG.txt;
	for{vmindex in 1..S}{
		if (l[vmindex] > 0) then{
			printf"\n vm %d (%g)", vmindex, l[vmindex] > resultsCG.txt;
			printf"\t items: "> resultsCG.txt;
			for{requestindex in 1..N}{
				if(a[requestindex, vmindex] == 1) then{
					printf" %d \t", requestindex > resultsCG.txt;
				}
			}
		}
	
	}
	printf"\n\n"> resultsCG.txt;
	
	
	#set the profit of each item equal to
	#the corresponding dual variable of the current master problem	
	for{i in 1..N} {
		if(cover[i].dual <=0) then let pi[i] := 0;
		else{
			let pi[i] := cover[i].dual;
		}
	}
		
	#solve the current pricing problem
	solve pricing;
	
	#print the cluster selected by the current pricing problem	
	printf "\n Candidate new cluster: \t" > resultsCG.txt;	
	for{i in 1..N}
	{
		if (u[i] = 1) then
			printf "%d  ",i > resultsCG.txt;
	}
	
	#print the profit of the candidate new cluster
	printf "\n reducedCost: %7.6f \n", reducedCost > resultsCG.txt;
	
	#check if the candidate new cluster makes the current dual solution infeasible
	if  (reducedCost >= 0) then
	{
		#current master problem solution optimal -> set flag to 0	
		let flag := 0;
		
		printf "\n Did not add cluster variable \n \n" > resultsCG.txt;
		printf"------------------------------------------------ \n" > resultsCG.txt;
		
		#terminate the column generation procedure
		break;
	}
	else
	{			
		#add the candidate new cluster (column/variable) to the master problem				
		let S:=S+1;	    			
		for{i in 1..N}
			let a[i,S]:= u[i];
		
		#print the current number of clusters in the master problem
	 	printf "\n Added cluster variable\n"> resultsCG.txt;
	    printf" S=%d \n", S > resultsCG.txt;
		printf" \n" > resultsCG.txt;
		printf"------------------------------------------------ \n" > resultsCG.txt;
		
		#current master problem solution not optimal -> set flag to 1	
		let flag:= 1;	
			 	
	}
}

#solve the master problem with the final set of clusters (columns/variables)
solve master;

#print the optimal objective function value of the final master problem
printf "\n final master objective: %7.6f \n", UsedVM > resultsCG.txt;
#print the number of columns (variables) of the final master problem	
printf "\n final number of columns: %7d \n", S > resultsCG.txt;

let toc := _ampl_user_time + _total_solve_user_time;
let CG_time := toc - tic;
printf "\n Total time is %s\n", CG_time > resultsCG.txt;

#check if the column generation procedure is forced to terminate
#before finding the optimal solution 
if (flag=1) then
	printf"\n Solution is not optimal: increase number of iterations!\n" > resultsCG.txt;	
else
	printf"\n Solution is optimal!\n" > resultsCG.txt;	

